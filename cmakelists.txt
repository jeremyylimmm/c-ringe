cmake_minimum_required(VERSION 3.10)

project(cringe)

if(MSVC)
  add_compile_options(/W4 /WX)
else()
  add_compile_options(-Wall -Wextra -Wpedantic -Werror)
endif()

set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED True)

file(MAKE_DIRECTORY "generated")

add_executable(dfa "meta/dfa.c")

set(DFA_LOCATION ${CMAKE_CURRENT_LIST_DIR}/generated/dfa.h)
set(TOKEN_KIND_LOCATION ${CMAKE_CURRENT_LIST_DIR}/generated/token_kind.h)

add_custom_command(
  OUTPUT ${DFA_LOCATION} ${TOKEN_KIND_LOCATION}
  COMMAND dfa ${DFA_LOCATION} ${TOKEN_KIND_LOCATION}
  DEPENDS dfa
  COMMENT "Generating dfa."
  VERBATIM
)

file(GLOB_RECURSE CRINGE_SOURCES "cringe/*.c" "cringe/*.h")
add_executable(cringe ${CRINGE_SOURCES} ${DFA_LOCATION} ${TOKEN_KIND_LOCATION})

target_include_directories(cringe PRIVATE "cringe" "generated")
target_include_directories(dfa PRIVATE "cringe")